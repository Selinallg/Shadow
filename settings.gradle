import java.nio.file.Files

rootProject.name = 'shadow'

copyLocalPropertiesForIncludeBuilds()
includeBuild 'shadow/coding'
includeBuild 'shadow/core'
includeBuild 'shadow/dynamic'


includeDev()

def includeDev() {
    include 'dev-constant',
            'dev-host',
            'dev-host-lib',
            'dev-manager',
            'dev-loader',
            'dev-runtime',
            'pinnedheaderexpandablelistview',
            'slidingmenu',
            'dev-base-lib',
            'dev-base',
            'dev-demo',
            'plugin-service-lib',// 插件serive demo
            'dev-app'
    project(':dev-constant').projectDir = file('host/dev-constant')
    project(':dev-host').projectDir = file('host/dev-host')
    project(':dev-host-lib').projectDir = file('host/dev-host-lib')
    project(':dev-manager').projectDir = file('host/dev-manager')

    project(':dev-loader').projectDir = file('plugins/dev-loader')
    project(':dev-runtime').projectDir = file('plugins/dev-runtime')
    project(':pinnedheaderexpandablelistview').projectDir = file('warehouse/third-party/pinnedheaderexpandablelistview')
    project(':slidingmenu').projectDir = file('warehouse/third-party/slidingmenu')
    project(':dev-base-lib').projectDir = file('plugins/zip/dev-base-lib')
    project(':dev-base').projectDir = file('plugins/zip/dev-base')
    project(':dev-app').projectDir = file('plugins/zip/dev-app')
    project(':dev-demo').projectDir = file('plugins/zip/dev-demo')
    project(':plugin-service-lib').projectDir = file('plugins/zip/plugin-service-lib')
}


/**
 * Android Studio当前不会为IncludeBuild创建包含sdk.dir的local.properties
 * 使得没有ANDROID_HOME或等效环境变量时仅依赖根目录的local.properties无法编译IncludeBuild。
 * 为了使此含有IncludeBuild的项目和其他不含有IncludeBuild的普通Android工程一样可以在
 * 只有根目录的local.properties情况下正常编译，用此任务复制local.properties。
 */
def copyLocalPropertiesForIncludeBuilds() {
    def rootFile = file('local.properties')
    if (rootFile.exists()) {
        ['coding', 'core', 'dynamic'].forEach {
            def includeBuildFile = file("shadow/${it}/local.properties")
            if (!includeBuildFile.exists()) {
                Files.copy(rootFile.toPath(), includeBuildFile.toPath())
            }
        }
    }
}

//include ':plugins:zip2:plugin-other'
//includeBuild ':plugins:zip2:plugin-other'
include 'plugin-other'
project(':plugin-other').projectDir = file('plugins/zip2/plugin-other')