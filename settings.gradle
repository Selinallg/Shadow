import java.nio.file.Files

rootProject.name = 'shadow'

copyLocalPropertiesForIncludeBuilds()
includeBuild 'shadow/coding'
includeBuild 'shadow/core'
includeBuild 'shadow/dynamic'


includeDev()

def includeDev() {
    include 'dev-constant',
            'dev-interface',
            'dev-host',
            'dev-host-lib',
            'dev-manager',
            'dev-loader',
            'dev-runtime',
            ':plugins:plugin-base-lib',
            ':plugins:plugin-base-apk',
            ':plugins:plugin-demo-apk',
            ':plugins:plugin-common-lib',// 插件通用库
            ':plugins:plugin-common-apk',// 插件通用库插件包
            'dev-app'
    project(':dev-constant').projectDir = file('host/dev-constant')
    project(':dev-interface').projectDir = file('host/dev-interface')
    project(':dev-host').projectDir = file('host/dev-host')
    project(':dev-host-lib').projectDir = file('host/dev-host-lib')
    project(':dev-manager').projectDir = file('host/dev-manager')

    project(':dev-loader').projectDir = file('plugins/dev-loader')
    project(':dev-runtime').projectDir = file('plugins/dev-runtime')
    project(':plugins:plugin-base-lib').projectDir = file('plugins/zip/plugin-base-lib')
    project(':plugins:plugin-base-apk').projectDir = file('plugins/zip/plugin-base-apk')
    project(':dev-app').projectDir = file('plugins/zip/dev-app')
    project(':plugins:plugin-demo-apk').projectDir = file('plugins/zip/plugin-demo-apk')
    project(':plugins:plugin-common-lib').projectDir = file('plugins/zip/plugin-common-lib')
    project(':plugins:plugin-common-apk').projectDir = file('plugins/zip/plugin-common-apk')
}


//include ':warehouse:third-party:slidingmenu'
include ':plugins:plugin-service-apk'
project(':plugins:plugin-service-apk').projectDir = file('plugins/zip/plugin-service-apk')

/**
 * Android Studio当前不会为IncludeBuild创建包含sdk.dir的local.properties
 * 使得没有ANDROID_HOME或等效环境变量时仅依赖根目录的local.properties无法编译IncludeBuild。
 * 为了使此含有IncludeBuild的项目和其他不含有IncludeBuild的普通Android工程一样可以在
 * 只有根目录的local.properties情况下正常编译，用此任务复制local.properties。
 */
def copyLocalPropertiesForIncludeBuilds() {
    def rootFile = file('local.properties')
    if (rootFile.exists()) {
        ['coding', 'core', 'dynamic'].forEach {
            def includeBuildFile = file("shadow/${it}/local.properties")
            if (!includeBuildFile.exists()) {
                Files.copy(rootFile.toPath(), includeBuildFile.toPath())
            }
        }
    }
}

//include ':plugins:zip2:plugin-other'
//includeBuild ':plugins:zip2:plugin-other'
include ':plugins:plugin-other-apk'
project(':plugins:plugin-other-apk').projectDir = file('plugins/zip2/plugin-other-apk')

include ':warehouse:third-party:slidingmenu'
include ':warehouse:third-party:pinnedheaderexpandablelistview'