buildscript {
    repositories {
        if (!System.getenv().containsKey("DISABLE_TENCENT_MAVEN_MIRROR")) {
            maven { url 'https://mirrors.tencent.com/nexus/repository/maven-public/' }
        } else {
            google()
            mavenCentral()
        }
    }

    dependencies {

        classpath 'com.tencent.shadow.core:runtime'
        classpath 'com.tencent.shadow.core:activity-container'
        classpath 'com.tencent.shadow.core:gradle-plugin'
        classpath "org.javassist:javassist:$javassist_version"

    }
}

//apply plugin: 'com.tencent.shadow.plugin'
plugins {
    id 'com.android.application'
    id 'com.tencent.shadow.plugin'
    id 'kotlin-android'
}



android {
    compileSdk project.COMPILE_SDK_VERSION

    defaultConfig {
        applicationId 'com.nolovr.shadow.core.plugin.app2'
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

    }

    // 将插件applicationId设置为和宿主相同
    productFlavors {
        plugin {
            applicationId project.DEV_HOST_APP_APPLICATION_ID
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    // 将插件的资源ID分区改为和宿主0x7F不同的值
    aaptOptions {
        additionalParameters "--package-id", "0x3E", "--allow-reserved-package-id"
    }
}

dependencies {

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "androidx.core:core-ktx:$core_ktx_version"
    implementation "androidx.appcompat:appcompat:$appcompat_version"
    implementation "com.google.android.material:material:$material_version"
    implementation "androidx.constraintlayout:constraintlayout:$constraintlayout_version"

    //Shadow Transform后业务代码会有一部分实际引用runtime中的类
    //如果不以compileOnly方式依赖，会导致其他Transform或者Proguard找不到这些类
    pluginCompileOnly 'com.tencent.shadow.core:runtime'
}



shadow {
    transform {
//        useHostContext = ['abc']
    }

    packagePlugin {
        pluginTypes {
            debug {
//                loaderApkConfig = new Tuple2('dev-loader-debug.apk', ':dev-loader:assembleDebug')
//                runtimeApkConfig = new Tuple2('dev-runtime-debug.apk', ':dev-runtime:assembleDebug')
                pluginApks {
                    pluginApk1 {
                        businessName = 'dev-plugin-xx'
                        partKey = 'dev-plugin-xx'
                        buildTask = ':plugins:zips:zip-plugin-other-apk:assemblePluginDebug'
                        apkPath = 'plugins/zip2/zip-plugin-other-apk/build/outputs/apk/plugin/debug/zip-plugin-other-apk-plugin-debug.apk'
                        hostWhiteList = ["com.tencent.shadow.sample.host.lib"]
                        dependsOn = ['plugin-common']
                    }
                }
            }

            release {
//                loaderApkConfig = new Tuple2('dev-loader-release.apk', ':dev-loader:assembleRelease')
//                runtimeApkConfig = new Tuple2('dev-runtime-release.apk', ':dev-runtime:assembleRelease')
                pluginApks {
                    pluginApk1 {
                        businessName = 'dev-plugin-xx'
                        partKey = 'dev-plugin-xx'
                        buildTask = ':plugins:zips:zip-plugin-other-apk:assemblePluginRelease'
                        apkPath = 'plugins/zip2/zip-plugin-other-apk/build/outputs/apk/plugin/debug/zip-plugin-other-apk-plugin-debug.apk'
                        hostWhiteList = ["com.tencent.shadow.sample.host.lib"]
                        dependsOn = ['plugin-common']
                    }
                }
            }
        }

        uuid = project.DEV_UUID_BUSINESS_SAMPLE

//        loaderApkProjectPath = 'plugins/dev-loader'
//        runtimeApkProjectPath = 'plugins/dev-runtime'

        archiveSuffix = System.getenv("PluginSuffix") ?: ""
        archivePrefix = project.DEV_ARCHIVE_PREFIX_ZIP_BS2
        destinationDir = "${getRootProject().getBuildDir()}"

        println("====destinationDir ${destinationDir}")

        version = 4
        compactVersion = [1, 2, 3]
        uuidNickName = "1.1.5"
    }
}