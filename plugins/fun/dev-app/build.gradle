buildscript {
    repositories {
        if (!System.getenv().containsKey("DISABLE_TENCENT_MAVEN_MIRROR")) {
            maven { url 'https://mirrors.tencent.com/nexus/repository/maven-public/' }
        } else {
            google()
            mavenCentral()
        }
    }

    dependencies {
        classpath 'com.tencent.shadow.core:runtime'
        classpath 'com.tencent.shadow.core:activity-container'
        classpath 'com.tencent.shadow.core:gradle-plugin'
        classpath "org.javassist:javassist:$javassist_version"
    }
}

apply plugin: 'com.android.application'
apply plugin: 'com.tencent.shadow.plugin'

android {
    compileSdkVersion project.COMPILE_SDK_VERSION

    defaultConfig {
        applicationId 'com.tencent.shadow.sample.plugin.app'
        minSdkVersion project.MIN_SDK_VERSION
        targetSdkVersion project.TARGET_SDK_VERSION
        versionCode project.VERSION_CODE
        versionName project.VERSION_NAME
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            signingConfig signingConfigs.create("release")
            signingConfig.initWith(buildTypes.debug.signingConfig)
        }
    }

    // 将插件applicationId设置为和宿主相同
    productFlavors {
        plugin {
            applicationId project.DEV_HOST_APP_APPLICATION_ID
        }
    }

    lintOptions {
        abortOnError false
    }

    // 将插件的资源ID分区改为和宿主0x7F不同的值
    aaptOptions {
        additionalParameters "--package-id", "0x7E", "--allow-reserved-package-id"
    }
}

dependencies {
    //注意dev-host-lib要用compileOnly编译而不打包在插件中。在packagePlugin任务中配置hostWhiteList允许插件访问宿主的类。
    pluginCompileOnly project(":dev-host-lib")
    normalImplementation project(":dev-host-lib")

    pluginCompileOnly project(":dev-base-lib")
    normalImplementation project(":dev-base-lib")

    //Shadow Transform后业务代码会有一部分实际引用runtime中的类
    //如果不以compileOnly方式依赖，会导致其他Transform或者Proguard找不到这些类
    pluginCompileOnly 'com.tencent.shadow.core:runtime'
}

preBuild.dependsOn(":dev-host-lib:jarDebugPackage")


def createDuplicateApkTask(buildType) {
    def apkDir = file("${getBuildDir()}/outputs/apk/plugin/$buildType")

    return tasks.create("duplicatePlugin${buildType.capitalize()}ApkTask", Copy) {
        group = 'build'
        description = "复制一个dev-app-plugin-${buildType}.apk用于测试目的"
        from(apkDir) {
            include("dev-app-plugin-${buildType}.apk")
            rename { "dev-app-plugin-${buildType}2.apk" }
        }
        into(apkDir)

    }.dependsOn(":dev-app:assemblePlugin${buildType.capitalize()}")
}

tasks.whenTaskAdded { task ->
    if (task.name == "assemblePluginDebug") {
        def createTask = createDuplicateApkTask('debug')
        task.finalizedBy(createTask)
    }
    if (task.name == "assemblePluginRelease") {
        def createTask = createDuplicateApkTask('release')
        task.finalizedBy(createTask)
    }
}


shadow {
    transform {
//        useHostContext = ['abc']
    }

    packagePlugin {
        pluginTypes {
            debug {
                loaderApkConfig = new Tuple2('dev-loader-debug.apk', ':dev-loader:assembleDebug')
                runtimeApkConfig = new Tuple2('dev-runtime-debug.apk', ':dev-runtime:assembleDebug')
                pluginApks {
                    pluginApk1 {
                        businessName = 'dev-plugin-app'
                        partKey = 'dev-plugin-app'
                        buildTask = ':dev-app:assemblePluginDebug'
                        apkPath = 'plugins/fun/dev-app/build/outputs/apk/plugin/debug/dev-app-plugin-debug.apk'
                        hostWhiteList = ["com.tencent.shadow.sample.host.lib"]
                        dependsOn = ['dev-base']
                    }
                    pluginApk2 {
                        businessName = 'dev-plugin-app2'
                        partKey = 'dev-plugin-app2'
                        buildTask = ':dev-app:assemblePluginDebug'
                        apkPath = 'plugins/fun/dev-app/build/outputs/apk/plugin/debug/dev-app-plugin-debug2.apk'
                        hostWhiteList = ["com.tencent.shadow.sample.host.lib"]
                        dependsOn = ['dev-base']
                    }
                    sampleBase {
                        businessName = 'dev-plugin-app'
                        partKey = 'dev-base'
                        buildTask = ':dev-base:assemblePluginDebug'
                        apkPath = 'plugins/fun/dev-base/build/outputs/apk/plugin/debug/dev-base-plugin-debug.apk'
                        hostWhiteList = ["com.tencent.shadow.sample.host.lib"]
                    }
                }
            }

            release {
                loaderApkConfig = new Tuple2('dev-loader-release.apk', ':dev-loader:assembleRelease')
                runtimeApkConfig = new Tuple2('dev-runtime-release.apk', ':dev-runtime:assembleRelease')
                pluginApks {
                    pluginApk1 {
                        businessName = 'dev-plugin-app'
                        partKey = 'dev-plugin-app'
                        buildTask = ':dev-app:assemblePluginRelease'
                        apkPath = 'plugins/fun/dev-app/build/outputs/apk/plugin/release/dev-app-plugin-release.apk'
                        hostWhiteList = ["com.tencent.shadow.sample.host.lib"]
                        dependsOn = ['dev-base']
                    }
                    pluginApk2 {
                        businessName = 'dev-plugin-app2'
                        partKey = 'dev-plugin-app2'
                        buildTask = ':dev-app:assemblePluginRelease'
                        apkPath = 'plugins/fun/dev-app/build/outputs/apk/plugin/release/dev-app-plugin-release2.apk'
                        hostWhiteList = ["com.tencent.shadow.sample.host.lib"]
                        dependsOn = ['dev-base']
                    }
                    sampleBase {
                        businessName = 'dev-plugin-app'
                        partKey = 'dev-base'
                        buildTask = ':dev-base:assemblePluginRelease'
                        apkPath = 'plugins/fun/dev-base/build/outputs/apk/plugin/release/dev-base-plugin-release.apk'
                        hostWhiteList = ["com.tencent.shadow.sample.host.lib"]
                    }
                }
            }
        }

        loaderApkProjectPath = 'plugins/dev-loader'
        runtimeApkProjectPath = 'plugins/dev-runtime'

        archiveSuffix = System.getenv("PluginSuffix") ?: ""
        archivePrefix = 'plugin'
        destinationDir = "${getRootProject().getBuildDir()}"

        version = 4
        compactVersion = [1, 2, 3]
        uuidNickName = "1.1.5"
    }
}
