buildscript {
    repositories {
        if (!System.getenv().containsKey("DISABLE_TENCENT_MAVEN_MIRROR")) {
            maven { url 'https://mirrors.tencent.com/nexus/repository/maven-public/' }
        } else {
            google()
            mavenCentral()
        }
    }

    dependencies {
        classpath 'com.tencent.shadow.core:runtime'
        classpath 'com.tencent.shadow.core:activity-container'
        classpath 'com.tencent.shadow.core:gradle-plugin'
        classpath "org.javassist:javassist:$javassist_version"
    }
}

apply plugin: 'com.android.application'
apply plugin: 'com.tencent.shadow.plugin'

android {
    compileSdk project.COMPILE_SDK_VERSION

    defaultConfig {
        applicationId 'com.nolovr.shadow.core.plugin.apk.common'
        minSdkVersion project.MIN_SDK_VERSION
        targetSdkVersion project.TARGET_SDK_VERSION
        versionCode project.VERSION_CODE
        versionName project.VERSION_NAME
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            signingConfig signingConfigs.create("release")
            signingConfig.initWith(buildTypes.debug.signingConfig)
        }
    }

    // 将插件applicationId设置为和宿主相同
    productFlavors {
        plugin {
            applicationId project.DEV_HOST_APP_APPLICATION_ID
        }
    }

    // 将插件的资源ID分区改为和宿主0x7F不同的值
    aaptOptions {
        additionalParameters "--package-id", "0x7E", "--allow-reserved-package-id"
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    pluginCompileOnly project(":plugins:plugin-common-lib")
    normalImplementation project(":plugins:plugin-common-lib")
    //Shadow Transform后业务代码会有一部分实际引用runtime中的类
    //如果不以compileOnly方式依赖，会导致其他Transform或者Proguard找不到这些类
    pluginCompileOnly 'com.tencent.shadow.core:runtime'
}

//preBuild.dependsOn(":plugin-common-lib:jarDebugPackage")

shadow {
    transform {
//        useHostContext = ['abc']
    }

    packagePlugin {
        pluginTypes {
            debug {
                loaderApkConfig = new Tuple2('dev-loader-debug.apk', ':dev-loader:assembleDebug')
                runtimeApkConfig = new Tuple2('dev-runtime-debug.apk', ':dev-runtime:assembleDebug')
                pluginApks {
                    pluginApk1 {
                        businessName = 'plugin-common'//businessName相同的插件，context获取的Dir是相同的。businessName留空，表示和宿主相同业务，直接使用宿主的Dir。
                        partKey = 'plugin-common'
                        buildTask = ':plugins:zips:zip-plugin-common-apk:assemblePluginDebug'
                        apkPath = 'plugins/zip-common/zip-plugin-common-apk/build/outputs/apk/plugin/debug/zip-plugin-common-apk-plugin-debug.apk'
                        hostWhiteList = ["com.nolovr.shadow.core.host.lib","com.tencent.shadow.sample.host.lib"]
                    }
                }
            }

            release {
                loaderApkConfig = new Tuple2('dev-loader-release.apk', ':dev-loader:assembleRelease')
                runtimeApkConfig = new Tuple2('dev-runtime-release.apk', ':dev-runtime:assembleRelease')
                pluginApks {
                    pluginApk1 {
                        businessName = 'plugin-common'
                        partKey = 'plugin-common'
                        buildTask = ':plugins:zips:zip-plugin-common-apk:assemblePlugin'
                        apkPath = 'plugins/zip-common/zip-plugin-common-apk/build/outputs/apk/plugin/release/zip-plugin-common-apk-plugin-release.apk'
                        hostWhiteList = ["com.nolovr.shadow.core.host.lib","com.tencent.shadow.sample.host.lib"]
                    }
                }
            }
        }

        uuid = project.DEV_UUID_BUSINESS_SAMPLE
        loaderApkProjectPath = 'plugins/dev-loader'
        runtimeApkProjectPath = 'plugins/dev-runtime'

        archiveSuffix = System.getenv("PluginSuffix") ?: ""
        archivePrefix = project.DEV_ARCHIVE_PREFIX_COMMON
        destinationDir = "${getRootProject().getBuildDir()}"

        version = 4
        compactVersion = [1, 2, 3]
        uuidNickName = "1.1.5"
    }
}