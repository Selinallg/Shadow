apply plugin: 'com.android.application'

android {
    compileSdkVersion project.COMPILE_SDK_VERSION
    defaultConfig {
        // com.tencent.shadow.dev.host
        applicationId project.DEV_HOST_APP_APPLICATION_ID
        minSdkVersion project.MIN_SDK_VERSION
        targetSdkVersion project.TARGET_SDK_VERSION
        versionCode project.VERSION_CODE
        versionName project.VERSION_NAME
    }
    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            signingConfig signingConfigs.create("release")
            signingConfig.initWith(buildTypes.debug.signingConfig)
        }
    }

    // 打包进 host 中的 assets
    sourceSets {
        debug {
            assets.srcDir('build/generated/assets/dev-manager/debug/')
            assets.srcDir('build/generated/assets/plugin-zip/debug/')
            assets.srcDir('build/generated/assets/plugin2-zip/debug/')
            assets.srcDir('build/generated/assets/pluginCommon-zip/debug/')
        }
        release {
            assets.srcDir('build/generated/assets/dev-manager/release/')
            assets.srcDir('build/generated/assets/plugin-zip/release/')
            assets.srcDir('build/generated/assets/plugin2-zip/release/')
            assets.srcDir('build/generated/assets/pluginCommon-zip/release/')
        }
    }
    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
}

dependencies {
    implementation "commons-io:commons-io:$commons_io_android_version"//sample-host从assets中复制插件用的
    implementation "org.slf4j:slf4j-api:$slf4j_version"

    implementation 'com.tencent.shadow.core:common'
    implementation 'com.tencent.shadow.dynamic:dynamic-host'
    implementation project(':dev-constant')
    implementation project(':dev-interface')
    implementation project(':dev-host-lib')
}

def createCopyTask(projectName, buildType, name, apkName, inputFile, taskName) {
    def outputFile = file("${getBuildDir()}/generated/assets/${name}/${buildType}/${apkName}")
    println("==== createCopyTask inputFile:${inputFile} --> ${inputFile.exists()}")
    println("==== createCopyTask outputFile:${outputFile}")
    outputFile.getParentFile().mkdirs()
    return tasks.create("copy${buildType.capitalize()}${name.capitalize()}Task", Copy) {
        group = 'build'
        description = "复制${name}到assets中."
        from(inputFile.getParent()) {
            include(inputFile.name)
            rename { outputFile.name }
        }
        into(outputFile.getParent())

    }.dependsOn("${projectName}:${taskName}")
}

def generateAssets(generateAssetsTask, buildType) {

    def moduleName = 'dev-manager'
    def pluginManagerApkFile = file(
            "${project(":dev-manager").getBuildDir()}" +
                    "/outputs/apk/${buildType}/" +
                    "${moduleName}-${buildType}.apk"
    )
    generateAssetsTask.dependsOn createCopyTask(
            ':dev-manager',
            buildType,
            moduleName,
            'pluginmanager.apk',
            pluginManagerApkFile,
            "assemble${buildType.capitalize()}"
    )

    def pluginZip = file("${getRootProject().getBuildDir()}/plugin-${buildType}.zip")
    println("pluginZip:${pluginZip}")
    generateAssetsTask.dependsOn createCopyTask(
            ':dev-app',
            buildType,
            'plugin-zip',
            "plugin-${buildType}.zip",
            pluginZip,
            "package${buildType.capitalize()}Plugin"
    )

    // :plugins:zip2:plugin-other
    def plugin2Zip = file("${getRootProject().getBuildDir()}/plugin2-${buildType}.zip")
    println("plugin2Zip:${plugin2Zip}")
    generateAssetsTask.dependsOn createCopyTask(
            ':plugins:zips:zip-plugin-other-apk',
            buildType,
            'plugin2-zip',
            "plugin2-${buildType}.zip",
            plugin2Zip,
            "package${buildType.capitalize()}Plugin"
    )

    // :plugins:zip:plugin-common-apk  必须与： archivePrefix = 'pluginCommon' 一致
    def pluginCommonZip = file("${getRootProject().getBuildDir()}/pluginCommon-${buildType}.zip")
//    println("plugin2Zip:${plugin2Zip}")
    generateAssetsTask.dependsOn createCopyTask(
            ':plugins:plugin-common-apk',
            buildType,
            'pluginCommon-zip',
            "pluginCommon-${buildType}.zip",
            pluginCommonZip,
            "package${buildType.capitalize()}Plugin"
    )
}

tasks.whenTaskAdded { task ->
    if (task.name == "generateDebugAssets") {
        generateAssets(task, 'debug')
    }
    if (task.name == "generateReleaseAssets") {
        generateAssets(task, 'release')
    }
}